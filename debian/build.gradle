subprojects {
  apply plugin: 'java'

  /* See the API Level defined in version_defaults.mk provided by
   * src:android-platform-build.
   */
  group = 'com.android'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
  version = '23'
  compileJava.options.encoding = 'UTF-8'

  configurations {
    doclava /* Classpaths for executing Doclava */
    forStubs /* For generating stubs */
  }

  dependencies {
    doclava 'com.google.doclava:doclava:debian'
  }

  task stubs {
    outputs.dir "${buildDir}/stubs"
    outputs.files "${buildDir}/stubs/**/*.java"
    compileJava.dependsOn 'stubs'
    doFirst {
      mkdir "${buildDir}/stubs"
      def sourceFiles = sourceSets.forStubs.allJava.join(' ')
      def classpath = configurations.forStubs.collect().join(':')
      def docletpath = "${configurations.doclava.collect().join(':')}:" +
                       '/usr/lib/jvm/default-java/lib/tools.jar'
      def stubsDir = "${buildDir}/stubs"
      def javaFlags = "-classpath $docletpath com.google.doclava.Doclava "
      /* From frameworks/base/Android.mk */
      def doclavaFlags = '-doclet com.google.doclava.Doclava ' +
                         '-encoding UTF-8 ' +
                         '-hdf sdk.version 6.0 -hdf sdk.rel.id 1 -hdf sdk.preview 0 ' +
                         '-hide 111 -hide 113 ' +
                         '-hidePackage com.android.org.conscrypt ' +
                         "-knowntags ${rootDir}/../frameworks/base/docs/knowntags.txt " +
                         '-nodocs ' +
                         '-offlinemode ' +
                         '-quiet ' +
                         "-stubs $stubsDir " +
                         '-XDignore.symbol.file ' +
                         "-api ${temporaryDir}/current.txt " +
                         "-removedApi ${temporaryDir}/removed.txt "
      if (classpath.size() > 0) doclavaFlags += "-classpath $classpath "
      copy {
        from apiListLocation
        into temporaryDir
      }
      def process = "java $javaFlags $sourceFiles $doclavaFlags".execute()
      process.waitForProcessOutput(System.out, System.err)
      if (process.exitValue() != 0) {
        throw new TaskExecutionException('Failed to generate stubs source.', null)
      }
    }
  }
}

project(':android') {
  ext.apiListLocation = "${rootDir}/../frameworks/base/api"
  sourceSets {
    forStubs {
      java {
        /* From <build/core/pathmap.mk> */
        srcDir "${buildDir}/aidl"
        srcDir "${rootDir}/../external/junit/src/junit/framework"
        srcDir "${rootDir}/../external/junit/src/junit/runner"
        srcDir "${rootDir}/../frameworks/base/core/java"
        srcDir "${rootDir}/../frameworks/base/drm/java"
        srcDir "${rootDir}/../frameworks/base/graphics/java"
        srcDir "${rootDir}/../frameworks/base/keystore/java"
        srcDir "${rootDir}/../frameworks/base/location/java"
        srcDir "${rootDir}/../frameworks/base/media/java"
        srcDir "${rootDir}/../frameworks/base/media/mca/effect/java"
        srcDir "${rootDir}/../frameworks/base/media/mca/filterfw/java"
        srcDir "${rootDir}/../frameworks/base/media/mca/filterpacks/java"
        srcDir "${rootDir}/../frameworks/base/opengl/java"
        srcDir "${rootDir}/../frameworks/base/rs/java"
        srcDir "${rootDir}/../frameworks/base/sax/java"
        srcDir "${rootDir}/../frameworks/base/telecomm/java"
        srcDir "${rootDir}/../frameworks/base/telephony/java"
        srcDir "${rootDir}/../frameworks/base/test-runner/src"
        srcDir "${rootDir}/../frameworks/base/wifi/java"
        srcDir "${rootDir}/../frameworks/opt/net/voip/src/java/android"
        srcDir "${rootDir}/../frameworks/opt/telephony/src/java/android"
        srcDir "${rootDir}/../libcore/dalvik/src/main/java"
        srcDir "${rootDir}/../libcore/json/src/main/java"
        srcDir "${rootDir}/../libcore/libart/src/main/java"
        srcDir "${rootDir}/../libcore/luni/src/main/java"
        srcDir "${rootDir}/../libcore/xml/src/main/java"
        srcDir "${rootDir}/out/R"
        /* From <libcore/Docs.mk> and
         * <development/build/tools/mk_sources_zip.py> */
        exclude 'android/​content/​pm/​PackageInfoLite.java'
        exclude 'android/​service/​voice/​IVoiceInteractionServ​ice.​class'
        exclude 'android/​service/​voice/​IVoiceInteractionSess​ion.​class'
        exclude 'android/​hardware/​soundtrigger/**'
        exclude 'android/​telephony/​DataConnectionRealTim​eInfo.java'
        exclude 'android/​telephony/​PreciseCallState.java'
        exclude 'android/​telephony/​PreciseDataConnection​State.java'
        
        
        exclude 'dalvik/system/profiler'
        exclude 'JUnit4TestAdapter.java'
        exclude 'JUnit4TestAdapterCache.java'
        exclude 'JUnit4TestCaseFacade.java'
        exclude 'libcore'
        exclude 'luni/src/main/java/java/text/IcuIteratorWrapper.java'
        exclude 'org/apache/harmony'
        exclude 'org/kxml2'
        exclude 'sun'
      }
    }
    main {
      java {
        srcDir "${buildDir}/stubs"
      }
      resources {
        srcDir "${rootDir}/out/framework-res"
      }
    }
  }

  /* No idea why these are not stripped by Doclava, but they are necessary for
     compiling some other classes in android.jar, so we will have to strip them
     here.
     
     These classes have been marked with "@hide", so Doclava should strip them.
     Probably bugs? */
  jar {
    exclude 'android/app/backup/RestoreDescription.class'
    exclude 'android/app/backup/RestoreSet.class'
    exclude 'android/bluetooth/BluetoothActivityEnergyInfo.class'
    exclude 'android/content/pm/KeySet.class'
    exclude 'android/content/pm/ManifestDigest.class'
    exclude 'android/content/pm/PackageCleanItem.class'
    exclude 'android/content/pm/PackageParser*.class'
    exclude 'android/content/pm/PackageUserState.class'
    exclude 'android/content/pm/ParceledListSlice.class'
    exclude 'android/content/pm/VerificationParams.class'
    exclude 'android/content/pm/VerifierDeviceIdentity.class'
    exclude 'android/content/pm/VerifierInfo.class'
    exclude 'android/net/NetworkStats*.class'
    exclude 'android/net/wifi/WifiActivityEnergyInfo.class'
    exclude 'android/os/BatteryStats*.class'
    exclude 'android/os/storage/DiskInfo.class'
    exclude 'android/os/storage/IMountService.class'
    exclude 'android/os/storage/IMountServiceListener.class'
    exclude 'android/os/storage/IMountShutdownObserver.class'
    exclude 'android/os/storage/IObbActionListener.class'
    exclude 'android/os/storage/StorageVolume.class'
    exclude 'android/os/storage/VolumeInfo.class'
    exclude 'android/os/storage/VolumeRecord.class'
    exclude 'android/telephony/VoLteServiceState.class'
    exclude 'android/util/IntArray.class'
    exclude 'android/view/InputChannel.class'
    exclude 'android/view/IWindow.class'
    exclude 'android/view/IWindowFocusObserver.class'
    exclude 'android/view/IWindowId.class'
    exclude 'android/view/IWindowSession.class'
    exclude 'android/widget/ActionMenuPresenter.class'
    exclude 'java/util/jar/StrictJarFile.class'
    exclude {
      if (it.isDirectory && it.getPath().startsWith('com/')) {
        if ('com/android/internal/util'.contains(it.getPath())) {
          false
        } else {
          true
        }
      } else {
        if (it.getPath().startsWith('com/')) {
          if (it.getPath().contains(
            'com/android/internal/util/Predicate.class'
          )) {
            false
          } else {
            true
          }
        }
      }
    }
    exclude {
      if (it.getPath().startsWith('android/content/pm/I')) {
        if (it.getPath().endsWith('InstrumentationInfo.class')) {
          false
        } else {
          true
        }
      } else {
        false
      }
    }
  }

}

project(':uiautomator') {
  ext.apiListLocation = "${rootDir}/../frameworks/base/cmds/uiautomator/api"
  stubs.dependsOn ':android:jar'
  dependencies {
    compile project(':android')
    forStubs project(':android')
  }

  sourceSets {
    main.java {
      srcDir "${buildDir}/stubs"
      /* By observing the upstream uiautomator.jar */
      include '**/Configurator.java'
      include '**/IAutomationSupport.java'
      include '**/UiAutomatorTestCase.java'
      include '**/UiCollection.java'
      include '**/UiDevice.java'
      include '**/UiObject.java'
      include '**/UiObjectNotFoundException.java'
      include '**/UiScrollable.java'
      include '**/UiSelector.java'
      include '**/UiWatcher.java'
    }
    forStubs.java {
      srcDir "${rootDir}/../frameworks/base/cmds/uiautomator/library/core-src"
      srcDir "${rootDir}/../frameworks/base/cmds/uiautomator/library/testrunner-src"
    }
  }
}
